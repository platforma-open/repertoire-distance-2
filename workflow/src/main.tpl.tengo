// "hello world"
wf := import("@platforma-sdk/workflow-tengo:workflow")
exec := import("@platforma-sdk/workflow-tengo:exec")
assets:= import("@platforma-sdk/workflow-tengo:assets")
xsv := import("@platforma-sdk/workflow-tengo:pframes.xsv")
ll := import("@platforma-sdk/workflow-tengo:ll")
pframes := import("@platforma-sdk/workflow-tengo:pframes")
canonical := import("@platforma-sdk/workflow-tengo:canonical")
pSpec := import("@platforma-sdk/workflow-tengo:pframes.spec")
self := import("@platforma-sdk/workflow-tengo:tpl")
maps := import("@platforma-sdk/workflow-tengo:maps")
json := import("json")

distanceSw := assets.importSoftware("@platforma-open/milaboratories.repertoire-distance-2.software:main")

self.awaitState("columnBundle", { wildcard: "*" }, "data", "InputsLocked")
self.awaitState("columnBundle", { wildcard: "*" }, "spec", "ResourceReady")

getLabel := func(mt) {
	if mt == "F1" {
		return "F1 Score"
	} else if mt == "F2" {
		return "F2 Score"
	} else if mt == "jaccard" {
		return "Jaccard Index"
	} else if mt == "D" {
		return "D Score"
	} else if mt == "correlation" {
		return "Correlation"
	}
	return mt;
}

getFullSpecs := func(abundanceSpec, metrics, blockId) {
	return {
		axes: [{
			column: "sample1",
			spec: {
				name: "pl7.app/sampleId",
				type: "String",
				domain: maps.merge(
					abundanceSpec.axesSpec[0].domain,
					{
						"pl7.app/overlap": "left"
					}
				),
				annotations: {
					"pl7.app/label": "Sample 1",
					"pl7.app/overlap": "left"
				}
			}
		},
		{
			column: "sample2",
			spec: {
				name: "pl7.app/sampleId",
				type: "String",
				domain: maps.merge(
					abundanceSpec.axesSpec[0].domain,
					{
						"pl7.app/overlap": "right"
					}
				),
				annotations: {
					"pl7.app/label": "Sample 2",
					"pl7.app/overlap": "right"
				}
			}
		},
		{
			column: "metric",
			spec: {
				name:"pl7.app/vdj/overlapMetric",
				type: "String",
				domain: abundanceSpec.axesSpec[0].domain,
				annotations: {
					"pl7.app/label": "Overlap Metric"
				}
			}
		}],
		columns: [{
			column: "value",
			spec: {
				name: "pl7.app/vdj/overlap",
				valueType: "Float",
				domain: maps.merge(
					abundanceSpec.domain,
					{
						"pl7.app/downsampling": canonical.encode(metrics[0].downsampling)
					}
				),
				annotations: {
					"pl7.app/label": "Value"
				}
			}
		}],
		storageFormat: "Binary",
		partitionKeyLength: 0
	}
}

getUniqueSpecs := func(abundanceSpec, metrics, blockId) {
	columns := []

	for index, metric in metrics {
		spec := {
			column: metric.type + "_" + metric.intersection,
			spec: {
				name: "pl7.app/overlap/" + metric.type,
				valueType: "Double",
				domain: maps.merge(
					abundanceSpec.domain,
					{
						"pl7.app/overlap/type": metric.type,
						"pl7.app/downsampling": canonical.encode(metric.downsampling)
					}
				),
				annotations: {
					"pl7.app/label": getLabel(metric.type) + " " + metric.intersection
				}
			}
		}
		columns = append(columns, spec)
	}

	return {
		axes: [{
			column: "sample1",
			spec: {
				name: "pl7.app/sampleId",
				type: "String",
				domain: maps.merge(
					abundanceSpec.axesSpec[0].domain,
					{
						"pl7.app/overlap": "left"
					}
				),
				annotations: {
					"pl7.app/label": "Sample 1",
					"pl7.app/overlap": "left"
				}
			}
		},
		{
			column: "sample2",
			spec: {
				name: "pl7.app/sampleId",
				type: "String",
				domain: maps.merge(
					abundanceSpec.axesSpec[0].domain,
					{
						"pl7.app/overlap": "right"
					}
				),
				annotations: {
					"pl7.app/label": "Sample 2",
					"pl7.app/overlap": "right"
				}
			}
		}],
		columns: columns,
		storageFormat: "Binary",
		partitionKeyLength: 0
	}
}

wf.prepare(func(args) {
	bundleBuilder := wf.createPBundleBuilder()
	bundleBuilder.addAnchor("main", args.abundanceRef)
	bundleBuilder.addMulti({
		name: "pl7.app/vdj/sequence",
		domain: {
			"pl7.app/vdj/feature": "CDR3"  // CDR1, CDR2, CDR3, or FR1
		},
		axes: [{anchor: "main", idx: 1}]
	}, "sequences");
	
	
	bundleBuilder.addMulti({
		name: "pl7.app/vdj/geneHit",
		axes: [{anchor: "main", idx: 1}]
	}, "genes");
	
	return  { 
		columns: bundleBuilder.build()
	}
})

wf.body(func(args) {
	blockId := wf.blockId().getDataAsJson()

	columns := args.columns
	sequences := columns.getColumns("sequences")
	genes := columns.getColumns("genes")
	abundanceSpec := columns.getSpec(args.abundanceRef)
	table := pframes.tsvFileBuilder()

	table.setAxisHeader("pl7.app/sampleId", "sampleId")
	table.add(columns.getColumn(args.abundanceRef), {header: "count"})

	for seqColumn in sequences {
		table.add(seqColumn, {header: seqColumn.spec.annotations["pl7.app/label"]})
	}

	for geneColumn in genes {
		if geneColumn.spec.domain["pl7.app/vdj/reference"] == "VGene" || geneColumn.spec.domain["pl7.app/vdj/reference"] == "JGene" {
			table.add(geneColumn, {header: geneColumn.spec.domain["pl7.app/vdj/reference"]})
		}
	}
	
	tsv := table.build()

	runDistance := exec.builder().
		software(distanceSw).
		arg("-i").arg("input.tsv").
		arg("-o1").arg("full_result.tsv").
		arg("-o2").arg("unique_result.tsv").
		writeFile("metrics.json", json.encode(args.metrics)).
		arg("-j").arg("metrics.json").
		addFile("input.tsv", tsv).
		saveFile("full_result.tsv").
		saveFile("unique_result.tsv").
		run()

	distanceResult := runDistance.getFile("full_result.tsv")
	distanceResultUnique := runDistance.getFile("unique_result.tsv")
	result := xsv.importFile(
		distanceResult,
		"tsv",
		getFullSpecs(abundanceSpec, args.metrics, blockId),
		{ splitDataAndSpec: true }
	)
	resultUnique := xsv.importFile(
		distanceResultUnique,
		"tsv",
		getUniqueSpecs(abundanceSpec, args.metrics, blockId),
		{ splitDataAndSpec: true }
	)

	//trace := pSpec.makeTrace(
	//	abundanceSpec,
	//	{
	//		type: "milaboratories.differential-repertoire-diversity",
	//		id: blockId,
	//		importance: 30, 
	//		label: "Repertoire Diversity"
	//	}
	//)

	pf := pframes.pFrameBuilder()
	for id, v in result {
		pf.add(id, v.spec, v.data)
	}
	pf = pf.build()

	pfUnique := pframes.pFrameBuilder()	
	for id, v in resultUnique {
		pfUnique.add(id, v.spec, v.data)
	}
	pfUnique = pfUnique.build()

	return {
		outputs: {
			pf: pframes.exportFrame(pf),
			pfUnique: pframes.exportFrame(pfUnique)
		},
		exports: {}
	}
})

