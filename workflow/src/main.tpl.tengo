// "hello world"
wf := import("@platforma-sdk/workflow-tengo:workflow")
exec := import("@platforma-sdk/workflow-tengo:exec")
assets:= import("@platforma-sdk/workflow-tengo:assets")
xsv := import("@platforma-sdk/workflow-tengo:pframes.xsv")
ll := import("@platforma-sdk/workflow-tengo:ll")
pframes := import("@platforma-sdk/workflow-tengo:pframes")
canonical := import("@platforma-sdk/workflow-tengo:canonical")
pSpec := import("@platforma-sdk/workflow-tengo:pframes.spec")
self := import("@platforma-sdk/workflow-tengo:tpl")
maps := import("@platforma-sdk/workflow-tengo:maps")
json := import("json")

distanceSw := assets.importSoftware("@platforma-open/milaboratories.repertoire-distance-2.software:main")

self.awaitState("columnBundle", { wildcard: "*" }, "data", "InputsLocked")
self.awaitState("columnBundle", { wildcard: "*" }, "spec", "ResourceReady")

getLabel := func(mt) {
	if mt == "F1" {
		return "F1 Score"
	} else if mt == "F2" {
		return "F2 Score"
	} else if mt == "jaccard" {
		return "Jaccard Index"
	} else if mt == "D" {
		return "D Score"
	} else if mt == "correlation" {
		return "Correlation"
	}
	return mt;
}

getFullSpecs := func(abundanceSpec, metrics) {
	return {
		axes: [{
			column: "sample1",
			spec: {
				name: "pl7.app/sampleId",
				type: "String",
				domain: maps.merge(
					abundanceSpec.axesSpec[0].domain,
					{
						"pl7.app/overlap": "left"
					}
				),
				annotations: {
					"pl7.app/label": "Sample 1"
				}
			}
		},
		{
			column: "sample2",
			spec: {
				name: "pl7.app/sampleId",
				type: "String",
				domain: maps.merge(
					abundanceSpec.axesSpec[0].domain,
					{
						"pl7.app/overlap": "right"
					}
				),
				annotations: {
					"pl7.app/label": "Sample 2"
				}
			}
		},
		{
			column: "metric",
			spec: {
				name:"pl7.app/vdj/overlapMetric",
				type: "String",
				domain: abundanceSpec.axesSpec[0].domain,
				annotations: {
					"pl7.app/label": "Overlap Metric"
				}
			}
		}],
		columns: [{
			column: "value",
			spec: {
				name: "pl7.app/vdj/overlap",
				valueType: "Float",
				domain: maps.merge(
					abundanceSpec.domain,
					{
						"pl7.app/downsampling": canonical.encode(metrics[0].downsampling)
					}
				),
				annotations: {
					"pl7.app/label": "Value"
				}
			}
		}],
		storageFormat: "Binary",
		partitionKeyLength: 0
	}
}

wf.prepare(func(args) {
	bundleBuilder := wf.createPBundleBuilder()
	bundleBuilder.addAnchor("main", args.abundanceRef)
	bundleBuilder.addMulti({
		name: "pl7.app/vdj/sequence",
		domain: {
			"pl7.app/vdj/feature": "CDR3"  // CDR1, CDR2, CDR3, or FR1
		},
		axes: [{anchor: "main", idx: 1}]
	}, "sequences");
	
	
	bundleBuilder.addMulti({
		name: "pl7.app/vdj/geneHit",
		axes: [{anchor: "main", idx: 1}]
	}, "genes");
	
	return  { 
		columns: bundleBuilder.build()
	}
})

wf.body(func(args) {
	blockId := wf.blockId().getDataAsJson()

	columns := args.columns
	sequences := columns.getColumns("sequences")
	genes := columns.getColumns("genes")
	abundanceSpec := columns.getSpec(args.abundanceRef)
	isSingleCell := abundanceSpec.axesSpec[1].name == "pl7.app/vdj/scClonotypeKey"
	table := pframes.tsvFileBuilder()

	table.setAxisHeader("pl7.app/sampleId", "sampleId")
	table.add(columns.getColumn(args.abundanceRef), {header: "count"})

	for seqColumn in sequences {
		if isSingleCell {
			if seqColumn.spec.domain["pl7.app/vdj/scClonotypeChain/index"] == "primary" {
				chain := seqColumn.spec.domain["pl7.app/vdj/scClonotypeChain"]
				alphabet := seqColumn.spec.domain["pl7.app/alphabet"]
				alphabetShort := (alphabet == "aminoacid") ? "aa" : "nt"
				table.add(seqColumn, {header: "CDR3 " + alphabetShort + " " + chain})
			}
		} else {
			table.add(seqColumn, {header: seqColumn.spec.annotations["pl7.app/label"]})
		}
	}

	for geneColumn in genes {
		if geneColumn.spec.domain["pl7.app/vdj/reference"] == "VGene" || geneColumn.spec.domain["pl7.app/vdj/reference"] == "JGene" {
			if isSingleCell {
				if geneColumn.spec.domain["pl7.app/vdj/scClonotypeChain/index"] == "primary" {
					ll.print("__THE_LOG__"+geneColumn.spec)
					chain := geneColumn.spec.domain["pl7.app/vdj/scClonotypeChain"]
					table.add(columns.getColumn(geneColumn.key), {header: geneColumn.spec.domain["pl7.app/vdj/reference"] + " " + chain})
				}
			} else {
				table.add(geneColumn, {header: geneColumn.spec.domain["pl7.app/vdj/reference"]})
			}
		}
	}
	
	tsv := table.build()

	runDistance := exec.builder().
		software(distanceSw).
		arg("-i").arg("input.tsv").
		arg("-o1").arg("full_result.tsv").
		arg("-o2").arg("unique_result.tsv").
		writeFile("metrics.json", json.encode(args.metrics)).
		arg("-j").arg("metrics.json").
		addFile("input.tsv", tsv).
		saveFile("full_result.tsv").
		saveFile("unique_result.tsv").
		run()

	distanceResult := runDistance.getFile("full_result.tsv")
	distanceResultUnique := runDistance.getFile("unique_result.tsv")
	result := xsv.importFile(
		distanceResult,
		"tsv",
		getFullSpecs(abundanceSpec, args.metrics),
		{ splitDataAndSpec: true }
	)

	trace := pSpec.makeTrace(
		abundanceSpec,
		{
			type: "milaboratories.repertoire-distance",
			id: blockId,
			importance: 30, 
			label: "Repertoire Distance 2"
		}
	)

	pf := pframes.pFrameBuilder()
	for id, v in result {
		pf.add(id, trace.inject(v.spec), v.data)
	}
	pf = pf.build()

	return {
		outputs: {
			pf: pframes.exportFrame(pf)
		},
		exports: {}
	}
})

